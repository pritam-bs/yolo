group = "space.prtlab.system_monitor"
version = "1.0-SNAPSHOT"

buildscript {
    ext.kotlin_version = "2.2.20"
    ext.agp_version = '8.13.2'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$agp_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')

if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    flutterRoot = System.getenv('FLUTTER_ROOT')
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url 'https://storage.googleapis.com/download.flutter.io'
        }
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

android {
    namespace = "space.prtlab.system_monitor"

    // Use SDK 35 for stability; 36 is currently in preview
    compileSdk = 36

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlin {
        compilerOptions {
            jvmTarget.set(org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_17)
        }
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }

    defaultConfig {
        minSdk = 24
    }

    dependencies {
        if (flutterRoot) {
            compileOnly files("$flutterRoot/bin/cache/artifacts/engine/android-arm/flutter.jar")
            compileOnly files("$flutterRoot/bin/cache/artifacts/engine/android-arm64/flutter.jar")
            compileOnly files("$flutterRoot/bin/cache/artifacts/engine/android-x86/flutter.jar")
            compileOnly files("$flutterRoot/bin/cache/artifacts/engine/android-x64/flutter.jar")
        } else {
            // If Flutter SDK not found, use embedding-release as fallback
            compileOnly 'io.flutter:flutter_embedding_release:1.0.0-1527ae0ec577a4ef50e65f6fefcfc1326707d9bf'
        }

        testImplementation("org.jetbrains.kotlin:kotlin-test")
        testImplementation("org.mockito:mockito-core:5.21.0")
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen {false}
                showStandardStreams = true
            }
        }
    }
}